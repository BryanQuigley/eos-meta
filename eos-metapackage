#!/usr/bin/perl -w
# -*- Mode: Perl; perl-indent-level: 8; indent-tabs-mode: t -*-

=head1 NAME

eos-metapackage - create metapackages from depends files

=cut

use strict;

use Debian::Debhelper::Dh_Lib;

=head1 SYNOPSIS

B<eos-metapackage> [S<B<debhelper options>>]

=head1 DESCRIPTION

eos-metapackage is a debhelper program that generates ${eos:Depends}
substitutions and adds them to substvars files.

The program reads dependency files for each package. The dependency
files are named after the package with -depends and -recommends
suffixes. They generate "${eos:Depends}" and "${eos:Recommends}"
variables to be substitued in the package's control file. Both files are
optional, so eos-metapackage can be called for all packages.

The format of the dependency files is one package per line. Lines
beginning with "#" are skipped.

=head1 EXAMPLES

eos-metapackage must be called before C<dh_installdeb>.

	override_dh_installdeb:
		./eos-metapackage
		dh_installdeb

=cut

init();

sub subst_depends {
	my $package=shift;
	my $type=shift;
	my $var=shift;
	my @deps;

	open DEPENDS, "$package-$type" or return;
	while (<DEPENDS>) {
		chomp;
		s/^\s*//; s/\s*$//;

		# Skip comments
		next if /^#/;

		push @deps, $_;
	}
	close DEPENDS;
	addsubstvar($package, $var, join(', ', @deps));
}

foreach my $package (@{$dh{DOPACKAGES}}) {
	subst_depends($package, "depends", "eos:Depends");
	subst_depends($package, "recommends", "eos:Recommends");
}

=head1 SEE ALSO

L<debhelper(7)>

=head1 AUTHOR

Colin Watson <cjwatson@ubuntu.com>

Copyright (C) 2009 Canonical Ltd., licensed under the GNU GPL v2 or later.

=cut
